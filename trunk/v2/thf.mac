|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|- thf.mac	v5.0
|- Written by Conradd of THF from Killians of PEQ.
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|

#TURBO 40

#INCLUDE Includes\thf_Setup.inc

SUB Main(modeSelect)

	/call initialize ${If[${Defined[modeSelect]},${modeSelect},]}

:MainLoop
	/if (${Debug}) /echo |- MainLoop ==>
	/call backgroundServices
	/if (!${activeTimer} && !${actionTaken}) {
		/if (${Bool[${Me.Class}]}) /call ${Me.Class.ShortName}_Main
		|- Additional macro code here.
		/if (!${actionTaken}) /call tertiaryServices
	}
	/if (${Debug}) {
		/echo <== MainLoop -|
		/delay 5
	}	
/goto :MainLoop
/RETURN

|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|- Background.inc's mainLoop hook.
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|- Calls background functions, as an extention of the main loop.
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
SUB backgroundServices
	/if (${Debug}) /echo |- backgroundServices ==>

	/if (!${Bool[${Me.Class}]}) /endmacro
	/if (${actionTaken}) /varset actionTaken FALSE
	/if (${autoAssistOn} && !${assisting}) /call checkAutoAssist
	/if (${currentZone} != ${Zone.ID}) /call checkZone
	/if (${Following} && !${assisting}) /call checkFollow
	
	/call backgroundEvents
	
	/doevents
	
	/call checkIdle
	/call checkActive
	/call checkCombat
	
	| - Check if I'm holding something.
	/if (!${Cursor.ID}) {
		/if (!${Me.Casting.ID} || ${Me.Casting.ID} && ${Me.Class.ShortName.Equal[BRD]}) {
			| Check LifeSupport
			/if (${lifeSupport.Size}) /callcheckLifeSupport
		
			| Maintain instant buffs.
			/if (${InstantBuffs.Size} && ${Me.FreeBuffSlots} > ${MaxBuffs}) /call buffInstants
			
			| Swap old items back.
			/if (${swapItemBack} && !${itemExchange_Timer}) {
				/call completePendingExchange
				/if (${Bool[${Macro.Return}]}) /varset swapItemBack FALSE
			}
		}
	} else {
		/chat ${channel} Holding ${Cursor.Name} on cursor.
	}

	/if (${Debug}) /echo <== backgroundServices -|
/RETURN

|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|- Background.inc's Tertiary mainloop hook.
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|- Calls clean-up and maintenance functions.
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
SUB tertiaryServices
	/if (${Debug}) /echo |- tertiaryServices ==>
	
	| Check pets.
	/if (${Pets} && !${suspend_Pets} && !${combatTimer} && !${activeTimer}) /call checkPetStatus
	
	| Buff Check.
	/if (${BuffCheck} && !${combatTimer} && !${activeTimer} && !${rebuffTimer}) /call buffCheck
	
	| Maintain macro timers.
	/if (${timerArray.Size}) /call checkMacroTimers

	/if (${Debug}) /echo <== tertiaryServices -|
/RETURN

|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|- Background events and run-times that are checked even while casting or otherwise considered active.
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
|- This function is checked constantly, included events and functions should have minimal character interaction.
|--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------|
SUB backgroundEvents
	
	/call assistBackgroundEvents
	/call basicsBackgroundEvents
	/call buffCheckBackgroundEvents
	/call castingBackgroundEvents
	/call healBackgroundEvents
	/call petBackgroundEvents
	/if (${Bool[${Me.Class}]}) /call ${Me.Class.ShortName}_backgroundEvents

/RETURN